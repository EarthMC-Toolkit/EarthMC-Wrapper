plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = project.maven_group
version = project.wrapper_version

allprojects {
    repositories {
        mavenCentral()

        // TODO: Is this repositories branch redundant?
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/earthmc-toolkit/earthmc-wrapper")
                credentials {
                    username = project.USERNAME
                    password = System.getenv("GITHUB_PACKAGES_TOKEN")
                }
            }
        }
    }
}

dependencies {
    //#region Implementations for 'main'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.jsoup:jsoup:1.15.4'

    implementation 'com.google.code.gson:gson:2.12.1'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp-brotli:4.12.0'
    //implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    //implementation 'com.github.jafarlihi:eemit:0.1.0' // TODO: Find maintained emitter lib or make own.
    //#endregion

    //#region Implementations for 'test'
    testCompileOnly "io.github.emcw:emc-wrapper:${version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    //#endregion

    //#region Plugins
    compileOnly 'org.jetbrains:annotations:24.0.1'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    //#endregion
}

// TODO: Could maybe do this in each JavaCompile task instead?
compileJava {
    options.compilerArgs.add("-Xlint:unchecked")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(source_jvm_version.toInteger())
        vendor = JvmVendorSpec.AZUL
    }

    withSourcesJar()
    //withJavadocJar()
}

//#region Task Configuration.
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = target_jvm_version.toInteger()
}

tasks.withType(Test).configureEach {
    javaLauncher.set(project.javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(target_jvm_version.toInteger())
    })
}
//#endregion

jar {
    // Will include every single runtime dependency
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    java {
        tasks.withType(Javadoc).configureEach {
            failOnError = false

            options.addStringOption("-release", target_jvm_version)

            options.addBooleanOption('Xdoclint:none', true)
            options.addStringOption('encoding', 'UTF-8')
            options.addStringOption('charSet', 'UTF-8')
        }

        withJavadocJar()
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/earthmc-toolkit/earthmc-wrapper")
            credentials {
                username = project.USERNAME
                password = System.getenv("GITHUB_PACKAGES_TOKEN")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = findProperty('maven_group')
            artifactId = findProperty('lib_name')
            version = findProperty('wrapper_version')

            from components.java
        }
    }
}

// Uncomment when we have tests setup with JUnit.
//test {
//    useJUnitPlatform()
//}